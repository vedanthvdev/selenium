load("@rules_dotnet//dotnet:defs.bzl", "csharp_library")
load("//common:defs.bzl", "copy_file")
load("//dotnet:defs.bzl", "DEFAULT_FRAMEWORKS", "devtools_version_targets", "framework", "nuget_pack", "nuget_push")
load("//dotnet:selenium-dotnet-version.bzl", "SE_VERSION")

[csharp_library(
    name = "%s" % framework_moniker,
    srcs = glob([
        "**/*.cs",
    ]) + devtools_version_targets(),
    resources = [
        "//javascript/atoms/fragments:find-elements.js",
        "//javascript/atoms/fragments:is-displayed.js",
        "//javascript/cdp-support:mutation-listener.js",
        "//javascript/webdriver/atoms:get-attribute.js",
        "//third_party/js/selenium:webdriver_json",
    ],
    target_frameworks = [framework_moniker],
    visibility = [
        "//dotnet:__subpackages__",
    ],
    deps = [
        framework(framework_moniker, "Microsoft.IdentityModel.Abstractions"),
        framework(framework_moniker, "Microsoft.IdentityModel.Tokens"),
        framework(framework_moniker, "Microsoft.NETCore.App.Ref"),
        framework(framework_moniker, "Newtonsoft.Json"),
        framework(framework_moniker, "System.Runtime"),
    ],
) for framework_moniker in DEFAULT_FRAMEWORKS]

copy_file(
    name = "props",
    src = "//dotnet/src/webdriver/build:Selenium.WebDriver.targets",
    out = "Selenium.WebDriver.targets",
)

copy_file(
    name = "transitive-props",
    src = "//dotnet/src/webdriver/build:Selenium.WebDriver.targets",
    out = "transitive.Selenium.WebDriver.targets",
)

nuget_pack(
    name = "pack-webdriver",
    files = {
        "//javascript/grid-ui:public/logo512.png": "images/logo.png",
        "//common/manager:selenium-manager-linux": "manager/linux/selenium-manager",
        "//common/manager:selenium-manager-macos": "manager/macos/selenium-manager",
        "//common/manager:selenium-manager-windows": "manager/windows/selenium-manager.exe",
        ":props": "build/Selenium.WebDriver.targets",
        ":transitive-props": "buildTransitive/Selenium.WebDriver.targets",
    },
    id = "Selenium.WebDriver",
    libs = {
        ":%s" % moniker: "WebDriver"
        for moniker in DEFAULT_FRAMEWORKS
    },
    nuget_spec = "WebDriver.nuspec",
    version = SE_VERSION,
)

nuget_push(
    name = "publish",
    src = ":pack-webdriver",
    api_key = "//dotnet:nuget-api-key",
)
